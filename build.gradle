apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'

repositories {
    mavenCentral()
    mavenRepo url: 'file:///Users/mcholick/Dropbox/repository'
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.5'
    compile 'org.jetbrains.idea:tasks-api:11.1.4'
    compile 'org.jetbrains.idea:openapi:11.1.4'
    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

sourceSets {
    main {
        groovy {
            srcDir 'src/main/groovy'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        groovy {
            srcDir 'src/test'
        }
    }
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'ProjectRootManager' }.each {
            it.parent().@'project-jdk-name'="IDEA IC-11.1"
            it.parent().@'project-jdk-type'="IDEA JDK"
        }
    }
}

idea.module.iml {
    whenMerged { module ->
        module.dependencies.each { dependency ->
            //gradle needs them for compile, but shouldn't duplicate for IntelliJ
            if(dependency.scope == 'COMPILE') {
                dependency.scope = 'PROVIDED'
            }
        }
    }
    withXml { provider ->
        provider.asNode().@type = 'PLUGIN_MODULE'
    }
}
